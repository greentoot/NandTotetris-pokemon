// Pokedex.jack - Collection de Pokemon capturés
class Pokedex {
    field Array captured;
    field int totalPokemon;
    field int capturedCount;
    
    constructor Pokedex new() {
        var int i;
        let totalPokemon = 12;
        let capturedCount = 0;
        let captured = Array.new(totalPokemon);
        
        let i = 0;
        while (i < totalPokemon) {
            let captured[i] = false;
            let i = i + 1;
        }
        
        return this;
    }
    
    method void capture(int pokemonType) {
        if (~captured[pokemonType]) {
            let captured[pokemonType] = true;
            let capturedCount = capturedCount + 1;
        }
        return;
    }
    
    method boolean isCaptured(int pokemonType) {
        return captured[pokemonType];
    }
    
    method int getCapturedCount() {
        return capturedCount;
    }
    
    method boolean isComplete() {
        return capturedCount = totalPokemon;
    }
    
    method void display() {
        var int i;
        
        do Output.moveCursor(1, 20);
        do Strings.printPokedex();
        do Output.moveCursor(2, 15);
        do Strings.drawseparationpokedex();
        
        // Colonne 1 (Pokémon 1-5)
        let i = 0;
        while (i < 5) {
            do Output.moveCursor(4 + i, 2);
            do Output.printInt(i + 1);
            do Output.printChar(46);  // .
            do Output.printChar(32);  // espace
            do printPokemonName(i);
            do Output.printChar(32);  // espace
            do Output.printChar(45);  // -
            do Output.printChar(32);  // espace
            
            if (captured[i]) {
                do Output.printChar(79);  // O
                do Output.printChar(75);  // K
            } else {
                do Output.printChar(63);  // ?
                do Output.printChar(63);
                do Output.printChar(63);
            }
            
            let i = i + 1;
        }
        
        // Colonne 2 (Pokémon 6-10)
        let i = 5;
        while (i < totalPokemon) {
            do Output.moveCursor(4 + (i - 5), 30);
            do Output.printInt(i + 1);
            do Output.printChar(46);  // .
            do Output.printChar(32);  // espace
            do printPokemonName(i);
            do Output.printChar(32);  // espace
            do Output.printChar(45);  // -
            do Output.printChar(32);  // espace
            
            if (captured[i]) {
                do Output.printChar(79);  // O
                do Output.printChar(75);  // K
            } else {
                do Output.printChar(63);  // ?
                do Output.printChar(63);
                do Output.printChar(63);
            }
            
            let i = i + 1;
        }
        
        do Output.moveCursor(11, 10);
        do Strings.printTotal();
        do Output.printInt(capturedCount);
        do Output.printChar(47);  // /
        do Output.printInt(totalPokemon);
        
        return;
    }
    
    method void printPokemonName(int type) {
        if (type = 0) { do Strings.printSalameche();}
        if (type = 1) { do Strings.printCarapuce();}
        if (type = 2) { do Strings.printBulbizarre();}
        if (type = 3) { do Strings.printPikachu();}
        if (type = 4) { do Strings.printRoucool();}
        if (type = 5) { do Strings.printRattata();}
        if (type = 6) { do Strings.printSabelette();}
        if (type = 7) { do Strings.printStari();}
        if (type = 8) {do Strings.printArcanin();}
        if (type = 9) { do Strings.printTaupiqueur();}
        if (type = 10) { do Strings.printTriopikeur();}
        if (type = 11) { do Strings.printNoadkoko();}
        return;
    }
    
    method void dispose() {
        do captured.dispose();
        do Memory.deAlloc(this);
        return;
    }
}