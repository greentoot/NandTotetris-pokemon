class Game {
    field MapManager mapManager;
    field Player player;
    field Battle battle;
    field Pokedex pokedex;
    field PnjManager pnjManager;
    field int gameState;
    field boolean running;
    field Pokemon wildPokemon;
    field TeamMenu teamMenu;
    field Pnj currentTrainer;  // Dresseur en cours de combat
    field Inventory inventory;
    field ItemMenu itemMenu;

    constructor Game new() {
        let mapManager = MapManager.new();
        let player = Player.new(10, 8);
        do player.setMapManager(mapManager);
        let battle = null;
        let wildPokemon = null;
        let pokedex = Pokedex.new();
        let pnjManager = PnjManager.new();
        let gameState = 0;
        let running = true;
        let teamMenu = TeamMenu.new(player.getTeam());
        let currentTrainer = null;
        let inventory = Inventory.new();
        let itemMenu = ItemMenu.new(inventory, player.getTeam());
        return this;
    }
    
    method void run() {
        var char key;
        
        do Random.init();
        do Screen.clearScreen();
        do mapManager.draw();
        do pnjManager.drawForMap(mapManager.getCurrentMapId());
        do player.draw();
        
        while (running) {
            let key = Keyboard.keyPressed();
            
            if (gameState = 0) {
                if (key = 131) { do handleMove(0, -1); }
                if (key = 133) { do handleMove(0, 1); }
                if (key = 130) { do handleMove(-1, 0); }
                if (key = 132) { do handleMove(1, 0); }
                if (key = 84) { do showTeamMenu(); }
                if (key = 80) { do showPokedex(); }
                if (key = 73) { do showInventory(); }
                if (key = 32) { do handleInteraction(); }
                if (key = 81) { let running = false; }
            } else {
                if (gameState = 1) {
                    do handleBattle(key);
                }
            }
            
            do Sys.wait(100);
        }
        return;
    }
    
    method void handleInteraction() {
        var Pnj pnj;
        var int currentMapId;
        
        let currentMapId = mapManager.getCurrentMapId();
        let pnj = pnjManager.checkInteraction(player.getX(), player.getY(), currentMapId);
        
        if (~(pnj = null)) {
            // Si c'est un dresseur non vaincu, lancer un combat
            if (pnj.getIsTrainer() & (~pnj.isDefeated())) {
                let currentTrainer = pnj;
                do startTrainerBattle(pnj);
            } else {
                // Sinon, dialogue normal
                do pnj.interact(player, pokedex);
                
                do Screen.clearScreen();
                do mapManager.draw();
                do pnjManager.drawForMap(currentMapId);
                do player.draw();
            }
        }
        
        return;
    }
    
    method void handleMove(int dx, int dy) {
        var int newX, newY, tile;
        var boolean encounter;
        var Map currentMap;
        var int currentMapId;
        
        let newX = player.getX() + dx;
        let newY = player.getY() + dy;
        
        let currentMap = mapManager.getCurrentMap();
        let currentMapId = mapManager.getCurrentMapId();
        let tile = currentMap.getTile(newX, newY);
        
        // Vérifier portails
        if (tile > 49) {
            do handlePortal(tile, newX, newY);
            return;
        }
        
        // Vérifier PNJ
        if (pnjManager.isPnjAtPosition(newX, newY, currentMapId)) {
            return;
        }
        
        // Mouvement normal
        if ((tile = 0) | (tile = 1) | (tile = 7)| (tile = 54)| (tile =18 )) {
            do player.erase();
            do player.move(dx, dy);
            do player.draw();
            
            // Rencontre aléatoire dans l'herbe (gameState 0 uniquement)
            if ((tile = 1) & (gameState = 0)) {
                let encounter = Random.chance(20);
                if (encounter) {
                    do startWildBattle();
                }
            }
        }
        
        return;
    }
    
    method void handlePortal(int portalId, int newX, int newY) {
        var int targetMapId;
        var Array portalPos;
        var Map targetMap;
        var int currentMapId;
        
        let currentMapId = mapManager.getCurrentMapId();
        let targetMapId = currentMapId;
        
        // Portail 50: Map 0 <-> Map 1
        if (portalId = 50) {
            if (currentMapId = 0) { let targetMapId = 1; }
            if (currentMapId = 1) { let targetMapId = 0; }
        }
        
        // Portail 51: Map 1 <-> Map 2
        if (portalId = 51) {
            if (currentMapId = 1) { let targetMapId = 2; }
            if (currentMapId = 2) { let targetMapId = 1; }
        }
        
        // Portail 53: Map 2 <-> Map 3
        if (portalId = 53) {
            if (currentMapId = 0) { let targetMapId = 3; }
            if (currentMapId = 3) { let targetMapId = 0; }
        }
        
        // Portail 54: Centre Pokémon <-> Map courante
        if (portalId = 54) {
            if (currentMapId = 4) {
                let targetMapId = 0; // Retour à la map 0
                let gameState = 0;
            } 
        }
        
        // Portail 55: Entrée Centre Pokémon depuis map 0
        if (portalId = 55) {
            let targetMapId = 4;
        }
        
        if (targetMapId = currentMapId) {
            return;
        }
        
        do Output.moveCursor(0, 0);
        do Strings.printTeleport();
        do Sys.wait(500);
        
        do mapManager.switchMap(targetMapId, portalId);
        let targetMap = mapManager.getCurrentMap();
        let portalPos = targetMap.findPortal(portalId);
        
        do player.setPosition(portalPos[0], portalPos[1]);
        do portalPos.dispose();
        
        do Screen.clearScreen();
        do mapManager.draw();
        do pnjManager.drawForMap(targetMapId);
        do player.draw();
        
        return;
    }
    
    method void startWildBattle() {
        var int pokemonType;
        
        let gameState = 1;
        
        if (~(wildPokemon = null)) {
            do wildPokemon.dispose();
        }
        
        let pokemonType = Random.randRange(0, 9);
        let wildPokemon = Pokemon.new(pokemonType, Random.randRange(3, 7));
        
        let battle = Battle.new(player.getPokemon(), wildPokemon, pokedex, player.getTeam(), inventory);
        do Screen.clearScreen();
        do battle.draw();
        
        return;
    }
    
method void startTrainerBattle(Pnj trainer) {
        var Pokemon trainerPokemon;
        var Team trainerTeam;
        
        let gameState = 1;
        
        // Récupérer le premier Pokémon du dresseur
        let trainerTeam = trainer.getTeam();
        let trainerPokemon = trainerTeam.getActive();
        
        let battle = Battle.new(player.getPokemon(), trainerPokemon, pokedex, player.getTeam(), inventory);
        do Screen.clearScreen();
        do battle.draw();
        
        // Afficher message de défi
        do Output.moveCursor(0, 0);
        do Output.printString("Un dresseur vous defie!");
        do Sys.wait(1500);
        
        do Screen.clearScreen();
        do battle.draw();
        
        return;
    }
    
    method void endBattle(boolean won, boolean captured) {
        var int currentMapId;
        
        // Si c'était un combat contre un dresseur
        if (~(currentTrainer = null)) {
            if (won) {
                do currentTrainer.setDefeated(true);
                do Output.moveCursor(0, 0);
                do Output.printString("Vous avez vaincu le dresseur!");
                do Sys.wait(2000);
            }
            let currentTrainer = null;
        }
        
        do battle.disposeWithoutEnemy();
        let battle = null;
        let gameState = 0;
        
        let currentMapId = mapManager.getCurrentMapId();
        do Screen.clearScreen();
        do mapManager.draw();
        do pnjManager.drawForMap(currentMapId);
        do player.draw();
        
        if (captured) {
            do Output.moveCursor(0, 0);
            do Strings.printPokemonCapture();
            do Sys.wait(2000);
            
            if (pokedex.isComplete()) {
                do winGame();
            }
        } else {
            if (won) {
                do Output.moveCursor(0, 0);
                do Strings.printVictoire();
                do Sys.wait(1000);
            }
        }
        
        return;
    }
    
    method void healAllPokemon() {
        var Team playerteamm ;
        let playerteamm = player.getTeam();
        do playerteamm.healAll();
        
        do Output.moveCursor(3, 2);
        do Output.printString("Soignons tous vos Pokemon...");
        do Sys.wait(2000);
        do Output.moveCursor(3, 2);
        do Output.printString("                              ");
        
        do Output.moveCursor(3, 2);
        do Output.printString("Voila! Ils sont en pleine forme!");
        do Sys.wait(2000);
        do Output.moveCursor(3, 2);
        do Output.printString("                                ");
        
        return;
    }
    method void showInventory() {
        var char key;
        var boolean exit;
        
        let exit = false;
        do itemMenu.display();
        
        while (~exit) {
            let key = Keyboard.keyPressed();
            
            if (key = 131) {
                do itemMenu.handleInput(key);
                do itemMenu.display();
                while (~(Keyboard.keyPressed() = 0)) {
                    do Sys.wait(50);
                }
            }
            if (key = 133) {
                do itemMenu.handleInput(key);
                do itemMenu.display();
                while (~(Keyboard.keyPressed() = 0)) {
                    do Sys.wait(50);
                }
            }
            if (key = 128) {
                do itemMenu.handleInput(key);
                do itemMenu.display();
                while (~(Keyboard.keyPressed() = 0)) {
                    do Sys.wait(50);
                }
            }
            if (key = 81) {
                if (itemMenu.isInPokemonSelection()) {
                    do itemMenu.setInPokemonSelection(false);
                    do itemMenu.display();
                } else {
                    let exit = true;
                }
                while (~(Keyboard.keyPressed() = 0)) {
                    do Sys.wait(50);
                }
            }
            
            do Sys.wait(100);
        }
        
        do Screen.clearScreen();
        do mapManager.draw();
        do pnjManager.drawForMap(mapManager.getCurrentMapId());
        do player.draw();
        
        return;
    }
    method void showPokedex() {
        do Screen.clearScreen();
        do pokedex.display();
        do Output.moveCursor(22, 0);
        do Strings.Appuyersurunetouche();
        
        while (Keyboard.keyPressed() = 0) {
            do Sys.wait(50);
        }
        while (~(Keyboard.keyPressed() = 0)) {
            do Sys.wait(50);
        }
        
        do Screen.clearScreen();
        do mapManager.draw();
        do pnjManager.drawForMap(mapManager.getCurrentMapId());
        do player.draw();
        
        return;
    }
    
    method void winGame() {
        let gameState = 2;
        do Screen.clearScreen();
        
        do Output.moveCursor(10, 15);
        do Strings.printFelicitations();
        do Output.moveCursor(12, 10);
        do Strings.printPokedexComplet();
        do Output.moveCursor(14, 8);
        do Strings.printMaitrePokemon();
        
        do Sys.wait(5000);
        let running = false;
        
        return;
    }
    
    method void showTeamMenu() {
        var char key;
        var boolean exit;
        
        let exit = false;
        do teamMenu.display();
        
        while (~exit) {
            let key = Keyboard.keyPressed();
            
            if (key = 131) {
                do teamMenu.handleInput(key);
                do teamMenu.display();
                while (~(Keyboard.keyPressed() = 0)) {
                    do Sys.wait(50);
                }
            }
            if (key = 133) {
                do teamMenu.handleInput(key);
                do teamMenu.display();
                while (~(Keyboard.keyPressed() = 0)) {
                    do Sys.wait(50);
                }
            }
            if (key = 128) {
                do teamMenu.handleInput(key);
                do teamMenu.display();
                while (~(Keyboard.keyPressed() = 0)) {
                    do Sys.wait(50);
                }
            }
            if (key = 81) {
                let exit = true;
                while (~(Keyboard.keyPressed() = 0)) {
                    do Sys.wait(50);
                }
            }
            if (key = 65) {
                do teamMenu.handleInput(key);
                do teamMenu.display();
                while (~(Keyboard.keyPressed() = 0)) {
                    do Sys.wait(50);
                }
            }
            if (key = 83) {
                do teamMenu.handleInput(key);
                do teamMenu.display();
                while (~(Keyboard.keyPressed() = 0)) {
                    do Sys.wait(50);
                }
            }
            
            do Sys.wait(100);
        }
        
        do Screen.clearScreen();
        do mapManager.draw();
        do pnjManager.drawForMap(mapManager.getCurrentMapId());
        do player.draw();
        
        return;
    }
    
// Méthode handleBattle() complète pour Game.jack
// Remplacez votre méthode handleBattle() actuelle par celle-ci

method void handleBattle(char key) {
    var int result;
    
    if (battle.isInSwitchMenu()) {
        // Menu de changement de Pokémon
        if (key = 131) {
            do battle.moveSwitchSelection(-1);
            do battle.drawSwitchMenu();
        }
        if (key = 133) {
            do battle.moveSwitchSelection(1);
            do battle.drawSwitchMenu();
        }
        if (key = 128) {
            let result = battle.switchPokemon(battle.getSwitchSelectedIndex());
            do battle.draw();
            
            if (result = 2) {
                do endBattle(false, false);
            }
        }
        if (key = 81) {
            do battle.setInSwitchMenu(false);
            do battle.draw();
        }
    } else {
        // Menu principal de combat
        
        // Attaques (touches 1-4)
        if ((key > 48) & (key < 53)) {
            let result = battle.attack(key - 49);
            do battle.draw();
            
            if (result = 1) { do endBattle(true, false); }
            if (result = 2) { do endBattle(false, false); }
            if (result = 3) { do endBattle(true, true); }
        }
        
        // INVENTAIRE (touche I = 73)
        if (key = 73) {
            do showInventory();
            do battle.draw();
        }
        
        // Capture seulement en combat sauvage (touche C = 67)
        if ((key = 67) & (currentTrainer = null)) {
            let result = battle.attemptCapture();
            do battle.draw();
            
            if (result = 3) {
                do endBattle(true, true);
            }
        }
        
        // Fuir (touche F = 70)
        if (key = 70) { 
            do endBattle(false, false); 
        }
        
        // Switch Pokemon (touche S = 83)
        if (key = 83) {
            do battle.setInSwitchMenu(true);
            do battle.drawSwitchMenu();
        }
    }
    
    return;
}
    
    method void dispose() {
        do mapManager.dispose();
        do teamMenu.dispose();
        do player.dispose();
        do inventory.dispose();
        do itemMenu.dispose();
        do pokedex.dispose();
        do pnjManager.dispose();
        if (~(wildPokemon = null)) {
            do wildPokemon.dispose();
        }
        if (~(battle = null)) {
            do battle.disposeWithoutEnemy();
        }
        do Memory.deAlloc(this);
        return;
    }
}