class PnjManager {
    field Array pnjList;
    field int pnjCount;
    field int maxPnj;
    
    constructor PnjManager new() {
        let maxPnj = 20;
        let pnjCount = 0;
        let pnjList = Array.new(maxPnj);
        
        do initPnj();
        
        return this;
    }
    
    method void initPnj() {
        // Map 0 - Village de départ
        let pnjList[0] = Pnj.new(12, 8, 35, 0, false);  // Enfant (non-combattant)
        let pnjCount = 1;
        
        // Map 1 - Route 1
        let pnjList[1] = Pnj.new(10, 10, 33, 1, true);  // Dresseur combattant
        let pnjList[2] = Pnj.new(15, 6, 33, 1, true);   // Dresseur combattant
        let pnjCount = 3;
        
        // Map 2 - Forêt
        let pnjList[3] = Pnj.new(8, 7, 34, 2, true);    // Scientifique combattant
        let pnjList[4] = Pnj.new(20, 10, 33, 2, true);  // Dresseur combattant
        let pnjCount = 5;
        
        // Map 3 - Ligue Pokémon

        
        // Map 4 - Centre Pokémon
        let pnjList[5] = Pnj.new(15, 6, 32, 4, false);  // Infirmière (non-combattant)
        let pnjCount = 6;
        
        return;
    }
    
    method void drawForMap(int mapId) {
        var int i;
        var Pnj pnj;
        
        let i = 0;
        while (i < pnjCount) {
            let pnj = pnjList[i];
            if (pnj.getMapId() = mapId) {
                do pnj.draw();
            }
            let i = i + 1;
        }
        
        return;
    }
    
    method Pnj checkInteraction(int playerX, int playerY, int mapId) {
        var int i;
        var Pnj pnj;
        var int dx, dy;
        
        let i = 0;
        while (i < pnjCount) {
            let pnj = pnjList[i];
            
            if (pnj.getMapId() = mapId) {
                let dx = Math.abs(playerX - pnj.getX());
                let dy = Math.abs(playerY - pnj.getY());
                
                if (((dx = 1) & (dy = 0)) | ((dx = 0) & (dy = 1))) {
                    do pnj.facePlayer(playerX, playerY);
                    return pnj;
                }
            }
            
            let i = i + 1;
        }
        
        return null;
    }
    
    method boolean isPnjAtPosition(int x, int y, int mapId) {
        var int i;
        var Pnj pnj;
        
        let i = 0;
        while (i < pnjCount) {
            let pnj = pnjList[i];
            if ((pnj.getMapId() = mapId) & pnj.isAtPosition(x, y)) {
                return true;
            }
            let i = i + 1;
        }
        
        return false;
    }
    
    method Pnj getPnjAt(int x, int y, int mapId) {
        var int i;
        var Pnj pnj;
        
        let i = 0;
        while (i < pnjCount) {
            let pnj = pnjList[i];
            if ((pnj.getMapId() = mapId) & pnj.isAtPosition(x, y)) {
                return pnj;
            }
            let i = i + 1;
        }
        
        return null;
    }
    
    method void dispose() {
        var int i;
        var Pnj pnj;
        
        let i = 0;
        while (i < pnjCount) {
            let pnj = pnjList[i];
            do pnj.dispose();
            let i = i + 1;
        }
        
        do pnjList.dispose();
        do Memory.deAlloc(this);
        return;
    }
}