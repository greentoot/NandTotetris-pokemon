class Player {
    field int x, y;
    field int size;
    field Team team;
    field MapManager mapManager;
    field int direction; // 0: bas, 1: haut, 2: gauche, 3: droite
    field int gameState; // Pour savoir où on est (0: monde, 3: centre)
    
    constructor Player new(int startX, int startY) {
        let x = startX;
        let y = startY;
        let size = 16;
        let direction = 0;
        let team = Team.new();
        let gameState = 0;
        return this;
    }
    
    method void setMapManager(MapManager manager) {
        let mapManager = manager;
        return;
    }
    
    method void setGameState(int state) {
        let gameState = state;
        return;
    }
    
    method void setPosition(int newX, int newY) {
        let x = newX;
        let y = newY;
        return;
    }
    
    method int getX() { return x; }
    method int getY() { return y; }
    method Pokemon getPokemon() { return team.getActive(); }
    method Team getTeam() { return team; }
    
    method void move(int dx, int dy) {
        let x = x + dx;
        let y = y + dy;
        
        // Mettre à jour la direction
        if (dy = -1) { let direction = 1; } // Haut
        if (dy = 1) { let direction = 0; }  // Bas
        if (dx = -1) { let direction = 2; } // Gauche
        if (dx = 1) { let direction = 3; }  // Droite
        
        return;
    }
    
    method void draw() {
        var int location;
        let location = ((y * size) * 32) + ((x * size) / 16);
        
        // Dessiner selon la direction
        if (direction = 0) { do Sprites.drawPlayerDown(location); }
        if (direction = 1) { do Sprites.drawPlayerUp(location); }
        if (direction = 2) { do Sprites.drawPlayerLeft(location); }
        if (direction = 3) { do Sprites.drawPlayerRight(location); }
        
        return;
    }
    
    method void erase() {
        var int px, py;
        var Map currentMap;
        
        let px = x * size;
        let py = y * size;
        
        // Toujours effacer en blanc d'abord
        do Screen.setColor(false);
        do Screen.drawRectangle(px, py, px + size - 1, py + size - 1);
        
        // Redessiner la tuile SEULEMENT si on est dans le monde (gameState = 0)
        if (gameState = 0) {
            if (~(mapManager = null)) {
                let currentMap = mapManager.getCurrentMap();
                do currentMap.redrawTile(x, y);
            }
        }
        
        return;
    }
    
    method void dispose() {
        do team.dispose();
        do Memory.deAlloc(this);
        return;
    }

}